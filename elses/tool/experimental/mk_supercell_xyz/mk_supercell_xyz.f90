! mk_supercell_xyz.f90 (build 20130402)
!
program main
  implicit none
  real(8),allocatable :: position(:,:)
  character(len=3),allocatable :: element_name(:)
  real(8) :: ax, ay, az
  integer :: noa, noa_new
  integer :: ix, iy, iz, k
  real(8) :: ddx, ddy, ddz
  integer :: nx, ny, nz
  character (len=8) :: chara1
  character(len=*), parameter :: filename_cell_info = 'input_supercell_size.txt'
  character(len=*), parameter :: filename_org       = 'structure_org.xyz'
  character(len=*), parameter :: filename_new       = 'structure_new.xyz'
  integer, parameter :: fd_cell_info = 29
  integer, parameter :: fd_org       = 30
  integer, parameter :: fd_new       = 31
!
  open (fd_cell_info, file=filename_cell_info)
  open (fd_org, file=filename_org)
  open (fd_new, file=filename_new)
!
  read(fd_cell_info,*) nx, ny, nz
  write(*,'(a)')          'Input file : input_supercell_size.txt'
  write(*,'(a,3i10)')     ' -->   nx, ny, nz  =', nx, ny, nz
!
  read(fd_org,*) noa, ax, ay, az
  write(*,'(a)')          'Input file : structure_org.xyz'
  write(*,'(a,i10)')      ' -->      N        =', noa
  write(*,'(a,3f20.10)')  ' -->  ax,ay,az [A] =', ax, ay, az
!
  noa_new=noa*nx*ny*nz
  write(*,'(a)')          'Output file : structure_new.xyz'
  write(*,'(a,i10)')      ' -->      N        =', noa_new
  write(*,'(a,3f20.10)')  ' -->  ax,ay,az [A] =', ax*dble(nx), ay*dble(ny), az*dble(nz)
!
  write(fd_new,'(i15, 3f20.10)') noa_new, ax*dble(nx), ay*dble(ny), az*dble(nz)
  write(fd_new,'(a)')'# generated by mk_supercell_xyz.f90 (build 20130402)'
!
  allocate (position(3,noa))
  allocate (element_name(noa))
!
  read(fd_org,*) chara1
  do k=1,noa
    read(fd_org,*) element_name(k), position(1:3,k)
  enddo
!
  do iz=0,nz-1
    do iy=0,ny-1
      do ix=0,nx-1
        do k=1,noa
          ddx=position(1,k)+ax*dble(ix)
          ddy=position(2,k)+ay*dble(iy)
          ddz=position(3,k)+az*dble(iz)
          write(fd_new,'(a2,3f30.20)') element_name(k), ddx, ddy, ddz
        enddo
      enddo
     enddo
  enddo
!
end program
